; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Canyon"
!define PRODUCT_VERSION "1.0"
!define PRODUCT_PUBLISHER "Canyon"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\Canyon.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

!define MUI_FINISHPAGE_SHOWREADME ""
!define MUI_FINISHPAGE_SHOWREADME_CHECKED
!define MUI_FINISHPAGE_SHOWREADME_TEXT "Add Firewall Exception"
!define MUI_FINISHPAGE_SHOWREADME_FUNCTION finishpageaction

; MUI 1.67 compatible ------
!include "MUI2.nsh"


Function LaunchLink
	SetOutPath $INSTDIR
	Exec "$INSTDIR\Canyon.exe"
FunctionEnd

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN
!define MUI_FINISHPAGE_RUN_FUNCTION "LaunchLink"
!define MUI_FINISHPAGE_LINK "Meow"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; Reserve files
ReserveFile /plugin InstallOptions.dll

; MUI end ------

RequestExecutionLevel admin 

!include LogicLib.nsh
!include WinMessages.nsh
 
!macro ShellExecWait verb app param workdir show exitoutvar ;only app and show must be != "", every thing else is optional
#define SEE_MASK_NOCLOSEPROCESS 0x40 
System::Store S
System::Call '*(&i60)i.r0'
System::Call '*$0(i 60,i 0x40,i $hwndparent,t "${verb}",t $\'${app}$\',t $\'${param}$\',t "${workdir}",i ${show})i.r0'
System::Call 'shell32::ShellExecuteEx(ir0)i.r1 ?e'
${If} $1 <> 0
	System::Call '*$0(is,i,i,i,i,i,i,i,i,i,i,i,i,i,i.r1)' ;stack value not really used, just a fancy pop ;)
	System::Call 'kernel32::WaitForSingleObject(ir1,i-1)'
	System::Call 'kernel32::GetExitCodeProcess(ir1,*i.s)'
	System::Call 'kernel32::CloseHandle(ir1)'
${EndIf}
System::Free $0
!if "${exitoutvar}" == ""
	pop $0
!endif
System::Store L
!if "${exitoutvar}" != ""
	pop ${exitoutvar}
!endif
!macroend

Name "${PRODUCT_NAME}"
OutFile "canyonsetup.exe"
InstallDir "$PROGRAMFILES\Canyon"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Section "MainSection" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File "Canyon.exe"
  File "Canyon.dll"
  File "pthreadGC2.dll"
  File "pthreadVC2.dll"
  File "QtCore4.dll"
  File "QtGui4.dll"
  File "QtNetwork4.dll"
  File "taplib.dll"
  File "CanyonUpdater.exe"
  CreateDirectory "$INSTDIR\imageformats"
  SetOutPath "$INSTDIR\imageformats"
  File "imageformats\qgif4.dll"
  File "imageformats\qico4.dll"
  SetOutPath "$INSTDIR"
  CreateDirectory "$SMPROGRAMS\Canyon"
  CreateShortCut "$SMPROGRAMS\Canyon\Canyon.lnk" "$INSTDIR\Canyon.exe"
  CreateShortCut "$DESKTOP\Canyon.lnk" "$INSTDIR\Canyon.exe"
  SetOutPath "$TEMP"
  File "tap-windows-9.9.2_3.exe"
  File "msvc_2010_redist_x86.exe"
  ExecWait '"$TEMP\tap-windows-9.9.2_3.exe" /S'	
  ExecWait '"$TEMP\msvc_2010_redist_x86.exe" /passive /norestart'
  Delete "$TEMP\tap-windows-9.9.2_3.exe"
  Delete "$TEMP\msvc_2010_redist_x86.exe"
  !insertmacro ShellExecWait "runas" '"$INSTDIR\Canyon.exe"' '"/setup"' "" ${SW_SHOW} $1
SectionEnd

Section -AdditionalIcons
  CreateShortCut "$SMPROGRAMS\Canyon\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\Canyon.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\Canyon.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


Function finishpageaction
        nsisFirewall::AddAuthorizedApplication "$INSTDIR\Canyon.exe" "Canyon"
	Pop $0
	IntCmp $0 0 +3
		MessageBox MB_OK "An error happened while adding Canyon to firewall exception list (result=$0)"
		Return
FunctionEnd

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Delete "$INSTDIR\Canyon.exe"
  Delete "$INSTDIR\Canyon.dll"
  Delete "$INSTDIR\pthreadGC2.dll"
  Delete "$INSTDIR\pthreadVC2.dll"
  Delete "$INSTDIR\QtCore4.dll"
  Delete "$INSTDIR\QtGui4.dll"
  Delete "$INSTDIR\QtNetwork4.dll"
  Delete "$INSTDIR\taplib.dll"
  Delete "$INSTDIR\CanyonUpdater.exe"
  Delete "$INSTDIR\imageformats\qgif4.dll"
  Delete "$INSTDIR\imageformats\qico4.dll"
  Delete "$INSTDIR\uninst.exe"

  Delete "$SMPROGRAMS\Canyon\Uninstall.lnk"
  Delete "$SMSTARTUP\Canyon.lnk"
  Delete "$DESKTOP\Canyon.lnk"
  Delete "$SMPROGRAMS\Canyon\Canyon.lnk"

  RMDir "$SMPROGRAMS\Canyon"
  RMDir "$INSTDIR\imageformats"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
  nsisFirewall::RemoveAuthorizedApplication "$INSTDIR\Canyon.exe"
	Pop $0
	IntCmp $0 0 +3
		MessageBox MB_OK "An error happened while removing program to firewall exception list (result=$0)"
		Return
SectionEnd